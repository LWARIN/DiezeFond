import java.util.logging.MemoryHandler
import fr.sma.speadl.ActionHandler
import fr.sma.speadl.MoveHandler
import fr.sma.speadl.GridUpdater
import fr.sma.speadl.EnvironmentRenderer
import fr.sma.speadl.GridDataProvider
import fr.sma.speadl.EnvironmentUpdater
import fr.sma.speadl.GuiConnector

namespace DiezeFond {

	/* Robot components */
	component RobotMemory {
		provides memoryHandler: MemoryHandler
	}
	
	component RobotActionManager {
		provides actionHandler: ActionHandler
		requires memoryHandler: MemoryHandler
		requires moveHandler: MoveHandler
	}
	
	/* Environment components */
	component EnvironmentMove {
		provides moveHandler: MoveHandler
		requires updateGrid: GridUpdater
	}
	
	component EnvironmentClock {
		requires renderEnvironment: EnvironmentRenderer
		requires actionHandler: ActionHandler
	}
	
	component GridManager {
		provides updateEnvironment: EnvironmentUpdater
		provides updateGrid: GridUpdater
		provides dataProvider: GridDataProvider
	}
	
	/* GUI components */
	component AppGUI {
		provides guiLink : GuiConnector
		requires updateEnvironment: EnvironmentUpdater
	}
	
	component EnvironmentGUI {
		requires guiLink : GuiConnector
		provides renderEnvironment: EnvironmentRenderer
		requires dataProvider: GridDataProvider
	}

	ecosystem EcoEnvironment {
		provides actionHandler : ActionHandler
		provides gridHandler : EnvironmentUpdater = gridManager.updateEnvironment
		provides environmentGuiHandler : EnvironmentRenderer = environmentGui.renderEnvironment
		species Robot(id: String) {
			requires environmentMoveHandler : MoveHandler
			provides robotActionHandler : ActionHandler = actionManager.actionHandler
			
			part memory: RobotMemory
			part actionManager: RobotActionManager {
				bind memoryHandler to memory.memoryHandler
				bind moveHandler to environmentMoveHandler
			}
		}

		part appGui : AppGUI {
			bind updateEnvironment to gridManager.updateEnvironment
		}

		part clock: EnvironmentClock {
			bind actionHandler to actionHandler
			bind renderEnvironment to environmentGui.renderEnvironment
		}

		part move: EnvironmentMove {
			bind updateGrid to gridManager.updateGrid
		}
		
		part gridManager : GridManager
		
		part environmentGui : EnvironmentGUI {
			bind dataProvider to gridManager.dataProvider
			bind guiLink to appGui.guiLink
		}
	}	
}