import fr.sma.core.Position
import fr.sma.fond.speadl.ActionManager
import fr.sma.fond.speadl.EcoRobotManager
import fr.sma.fond.speadl.GridManager
import fr.sma.fond.speadl.InitManager
import fr.sma.fond.speadl.MemoryManager
import fr.sma.fond.speadl.GridProvider
import fr.sma.fond.speadl.IGuiUpdate

namespace Fond {
	
	component RobotMemory {
		provides memoryManager : MemoryManager
	}
	
	component  RobotActionManager {
		provides actionManager : ActionManager
		requires memoryManager : MemoryManager
	} 
	
	component Grid {
		provides gridManager : GridManager
		provides gridProvider : GridProvider
	} 
	
	component Clock {
		requires ecoRobot : EcoRobotManager
		requires guiUpdater : IGuiUpdate
	}
	
	ecosystem EcoRobot {
		requires gridProvider : GridProvider
		provides ecoRobotManager : EcoRobotManager
		
		species Robot (id : String, position : Position) {
			part memory: RobotMemory
			
			part actionManager: RobotActionManager {
				bind memoryManager to memory.memoryManager
			}
			
			provides robotActionHandler : ActionManager = actionManager.actionManager
		}
	}
	
	component Plant {
		provides initManager : InitManager
		provides ecoRobotManager : EcoRobotManager = robots.ecoRobotManager
		provides gridProvider : GridProvider = grid.gridProvider
		
		part grid : Grid
		
		part robots : EcoRobot {
			bind gridProvider to grid.gridProvider
		}		
	}
	
	component Gui {
		provides guiUpdater : IGuiUpdate	
		requires gridProvider : GridProvider	
	}
	
	component App {
		provides initManager : InitManager = plant.initManager
		
		part plant : Plant
		
		part gui : Gui {
			bind gridProvider to plant.gridProvider
		}
		
		part clock : Clock {
			bind ecoRobot to plant.ecoRobotManager
			bind guiUpdater to gui.guiUpdater
		}
	}
}