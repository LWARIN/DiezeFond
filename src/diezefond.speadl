import fr.sma.fond.core.Position
import fr.sma.fond.speadl.ActionManager
import fr.sma.fond.speadl.EcoRobotManager
import fr.sma.fond.speadl.GridManager
import fr.sma.fond.speadl.InitManager
import fr.sma.fond.speadl.MemoryManager
import fr.sma.fond.speadl.GridProvider
import fr.sma.fond.speadl.IGuiUpdate
import fr.sma.fond.speadl.Log

namespace Fond {
	
	component RobotMemory {
		provides memoryManager : MemoryManager
	}
	
	component  RobotActionManager {
		provides actionManager : ActionManager
		requires memoryManager : MemoryManager
	} 
	
	component Grid {
		provides gridManager : GridManager
		provides gridProvider : GridProvider
		requires log : Log
	} 
	
	component Clock {
		requires ecoRobot : EcoRobotManager
		requires guiUpdater : IGuiUpdate
		requires log : Log
	}
	
	ecosystem EcoRobot {
		requires gridProvider : GridProvider
		provides ecoRobotManager : EcoRobotManager
		requires log : Log
		
		species Robot (id : String, position : Position) {
			part memory: RobotMemory
			
			part actionManager: RobotActionManager {
				bind memoryManager to memory.memoryManager
			}
			
			provides robotActionHandler : ActionManager = actionManager.actionManager
		}
	}
	
	component Plant {
		provides initManager : InitManager
		provides ecoRobotManager : EcoRobotManager = robots.ecoRobotManager
		provides gridProvider : GridProvider = grid.gridProvider
		requires log : Log
		
		part grid : Grid {
			bind log to log
		}
		
		part robots : EcoRobot {
			bind gridProvider to grid.gridProvider
			bind log to log
		}		
	}
	
	component Gui {
		provides guiUpdater : IGuiUpdate	
		requires gridProvider : GridProvider	
	}
	
	component Logger {
		provides log: Log
	}
	
	component App {
		provides initManager : InitManager = plant.initManager
		provides log : Log = logger.log
		
		part logger : Logger
				
		part plant : Plant {
			bind log to log
		}
		
		part gui : Gui {
			bind gridProvider to plant.gridProvider
		}
		
		part clock : Clock {
			bind ecoRobot to plant.ecoRobotManager
			bind guiUpdater to gui.guiUpdater
			bind log to log
		}
	}
}