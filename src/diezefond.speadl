import fr.sma.fond.core.Position
import fr.sma.fond.speadl.ActionManager
import fr.sma.fond.speadl.EcoRobotManager
import fr.sma.fond.speadl.GridManager
import fr.sma.fond.speadl.GridProvider
import fr.sma.fond.speadl.IGuiLogger
import fr.sma.fond.speadl.IGuiUpdate
import fr.sma.fond.speadl.InitManager
import fr.sma.fond.speadl.Log
import fr.sma.fond.speadl.MemoryManager
import fr.sma.fond.speadl.ClockManager

namespace Fond {
	
	component RobotMemory {
		provides memoryManager : MemoryManager
	}
	
	component  RobotActionManager {
		requires memoryManager : MemoryManager
		
		provides actionManager : ActionManager
	} 
	
	component Grid {
		requires log : Log
		
		provides gridManager : GridManager
		provides gridProvider : GridProvider
	} 
	
	component Clock {
		requires ecoRobot : EcoRobotManager
		requires guiUpdater : IGuiUpdate
		requires log : Log
		provides clockManager : ClockManager
	}
	
	ecosystem EcoRobot {
		requires gridProvider : GridProvider
		provides ecoRobotManager : EcoRobotManager
		requires log : Log
		
		species Robot (id : String, position : Position) {
			part memory: RobotMemory
			
			part actionManager: RobotActionManager {
				bind memoryManager to memory.memoryManager
			}
			
			provides robotActionHandler : ActionManager = actionManager.actionManager
		}
	}
	
	component Plant {
		requires log : Log
		
		provides initManager : InitManager
		provides ecoRobotManager : EcoRobotManager = robots.ecoRobotManager
		provides gridProvider : GridProvider = grid.gridProvider
		
		part grid : Grid {
			bind log to log
		}
		
		part robots : EcoRobot {
			bind gridProvider to grid.gridProvider
			bind log to log
		}		
	}
	
	component Gui {
		requires gridProvider : GridProvider
		requires clockManager : ClockManager
		
		provides guiUpdater : IGuiUpdate	
		provides guiLogger : IGuiLogger	
	}
	
	component Logger {
		provides log : Log
	}
	
	component App {
		provides initManager : InitManager = plant.initManager
		provides log : Log = logger.log
		provides guiLog : IGuiLogger = gui.guiLogger
		
		part logger : Logger
				
		part plant : Plant {
			bind log to log
		}
		
		part gui : Gui {
			bind gridProvider to plant.gridProvider
			bind clockManager to clock.clockManager
		}
		
		part clock : Clock {
			bind ecoRobot to plant.ecoRobotManager
			bind guiUpdater to gui.guiUpdater
			bind log to log
		}
	}
}